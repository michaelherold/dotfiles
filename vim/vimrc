" => General {{{
"""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""

" Use vim settings, rather than vi settings (much better!)
" Must be first, because it changes other options as a side effect
set nocompatible

" Set leader to ' '
let mapleader = " "
let g:mapleader = " "

" }}}
" => Plugins {{{
"""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
call plug#begin('~/.vim/plugged')

" Colorschemes
Plug 'connorholyday/vim-snazzy'

" Utilities
Plug 'christoomey/vim-tmux-navigator'
Plug 'editorconfig/editorconfig-vim'
Plug 'janko-m/vim-test'
Plug '/usr/local/opt/fzf' | Plug 'junegunn/fzf.vim'
Plug 'junegunn/vim-easy-align'
Plug 'reedes/vim-textobj-quote' | Plug 'kana/vim-textobj-user'
Plug 'rking/ag.vim', { 'on': ['Ag'] }
Plug 'scrooloose/nerdtree', { 'on': ['NERDTreeToggle', 'NERDTreeFind'] } | Plug 'Xuyuanp/nerdtree-git-plugin'
Plug 'sheerun/vim-polyglot'
Plug 'sickill/vim-pasta'
Plug 'sjl/gundo.vim'
Plug 'skwp/greplace.vim'
Plug 'tpope/vim-commentary'
Plug 'tpope/vim-dispatch'
Plug 'tpope/vim-endwise'
Plug 'tpope/vim-fugitive'
Plug 'tpope/vim-liquid'
Plug 'tpope/vim-ragtag'
Plug 'tpope/vim-rhubarb'
Plug 'tpope/vim-surround'
Plug 'w0rp/ale'
Plug 'wakatime/vim-wakatime'

" Markup
Plug 'gregsexton/MatchTag', { 'for': ['html'] }
Plug 'othree/html5.vim', { 'for': ['html'] }
Plug 'vim-scripts/closetag.vim', { 'for': ['html', 'eruby'] }

" Miscellaneous Languages
Plug 'vim-scripts/bats.vim', { 'for': ['bats'] }

" UI
Plug 'airblade/vim-gitgutter'
Plug 'itchyny/lightline.vim'
Plug 'ryanoasis/vim-devicons'

" Styles
Plug 'ap/vim-css-color', { 'for': ['css', 'stylus', 'scss'] }
Plug 'hail2u/vim-css3-syntax', { 'for': ['css'] }
Plug 'cakebaker/scss-syntax.vim', { 'for': ['scss'] }
Plug 'wavded/vim-stylus', { 'for': ['stylus'] }

" TMux Integrations
Plug 'blueyed/vim-diminactive'
Plug 'tmux-plugins/vim-tmux-focus-events'

" Writing
Plug 'junegunn/goyo.vim', { 'on': 'Goyo' }
Plug 'junegunn/limelight.vim', { 'on': ['Limelight', 'Limelight!'] }
Plug 'tpope/vim-markdown'
Plug 'nelstrom/vim-markdown-folding', { 'for': ['markdown'] }

call plug#end()
" }}}
" => Colors and Fonts {{{
"""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""

if (has("termguicolors"))
  if (!has("nvim"))
    let &t_8f = "\<Esc>[38;2;%lu;%lu;%lum"
    let &t_8b = "\<Esc>[48;2;%lu;%lu;%lum"
  endif
  set termguicolors
endif

set background=dark
syntax on  " switch syntax highlighting on for color term

colorscheme snazzy

" }}}
" => Editing Behavior {{{
"""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""

if has('nvim')
  set backupdir=$XDG_CACHE_HOME/neovim/backup
  set directory=$XDG_CACHE_HOME/neovim/swap
  set undodir=$XDG_CACHE_HOME/neovim/undo
  set viminfo=%,'9999,s512,n$XDG_CACHE_HOME/neovim/viminfo
else
  set backupdir=$XDG_CACHE_HOME/vim/backup
  set directory=$XDG_CACHE_HOME/vim/swap
  set undodir=$XDG_CACHE_HOME/vim/undo
  set viminfo=%,'9999,s512,n$XDG_CACHE_HOME/vim/viminfo
endif

set autoindent              " always set autoindenting on
set autowrite               " automatically :write before running commands
set backspace=2             " allow backspacing over everything in insert mode
set backup                  " enable backup files
set backupext=-vimbackup    " and name them something easy to see
set copyindent              " copy the previous indentation on autoindenting
set cursorline              " underline the current line, for quick orientation
set diffopt+=algorithm:patience " use the patient algorithm for diffing
set diffopt+=indent-heuristic   " use heuristics for intelligent diffing
set display+=lastline       " show as much as possible of the last line
set encoding=utf-8 nobomb   " UTF-8 by default without BOM
set expandtab               " expand tabs by default (overloadable per file type)
set fileformats=unix,dos,mac  " set the ordering for file formats
set formatoptions=
set formatoptions+=1        " when wrapping paragraphs, don't end lines with 1-letter words
set formatoptions+=2        " use indent from 2nd line of a paragraph
set formatoptions+=c        " format comments
set formatoptions+=l        " don't break lines that are already long
set formatoptions+=n        " recognize numbered lines
set formatoptions+=o        " make comment when using o or O from comment line
set formatoptions+=q        " recognize comments with gq
set formatoptions+=r        " continue comments by default
set gdefault                " search/replace 'globally' by default
set hidden                  " hide buffers instead of closing them
set history=50              " remember more commands and search history
set hlsearch                " highlight search terms
set ignorecase              " ignore case when searching
set incsearch               " show search matches as you type
set laststatus=2            " tell vim to always put a status line in
set lazyredraw              " don't update the display when running macros
set magic                   " change the way backslahes are used in search patterns
set modeline                " enable modelines
set mouse=a                 " enable using the mouse if terminal emulator supports it
set noerrorbells            " don't beep
set nojoinspaces            " only insert space in join after punctuation
set nolist                  " don't show invisible characters by default
set noshowmode              " don't show the current mode in command line
set nostartofline           " don't reset cursor to start of line when moving around
set nowrap                  " don't wrap lines
set number                  " always show line numbers
set relativenumber          " show relative line numbers (having both is a hybrid approach)
set report=0                " always report changed lines
set ruler                   " show location on command line
set scrolloff=4             " keep 4 lines off the edges of the screen when scrolling
set shiftround              " use multiple of shiftwidth when indenting with '>' and '<'
set shiftwidth=2            " number of spaces to use for auto indenting
set showcmd                 " show (partial) command in the last line of the screen
set showmatch               " set show matching parentheses
set showmode                " always show what mode we're in
set smartcase               " ignore case only if search pattern all lowercase
set smarttab                " insert tabs on line start according to shiftwidth
set softtabstop=2           " when hitting <BS>, pretend like tab is removed, even if spaces
set splitbelow              " open horizontal window splits below current one
set splitright              " open veritcal window splits to the right of the current one
set switchbuf=useopen       " reveal already opened files from quickfix
set tabstop=2               " a tab is two spaces
set termencoding=utf-8
set title                   " change the terminal's title
set ttyfast                 " always redraw on character change since we're on fast computers now
set undofile                " try out undo files again for persistence across opens
set undolevels=1000         " use many mucho levels of undo
set updatecount=100         " write swapfiles after 100 characters changed
set virtualedit=block       " allow cursor to go in to 'invalid' places
set visualbell              " don't beep
set wildignore+=*.swp,*.bak,*.pyc,*.class
set wildignore+=*/.git/*
set wildignore+=*/bower_components/*,*/node_modules/*
set wildignore+=bundle/**,vendor/bundle/**
set wildignore+=coverage/*
set wildignore+=tmp/**,*.rbc,*.rbx,*.scssc,*.sassc
set wildmenu                " make tab completion for files/buffer bash-like
set wildmode=list:longest   " complete only to the point of ambiguity
set wrapscan                " searches wraps around end-of-line

set list                    " show non-printable characters
if has('multi_byte') && &encoding ==# 'utf-8'
  let &listchars = 'tab:▸ ,extends:❯,precedes:❮,nbsp:±,trail:·'
else
  let &listchars = 'tab:> ,extends:>,precedes:<,nbsp:.'
endif

" Sane regular expressions, via Steve Losh
" See http://stevelosh.com/blog/2010/09/coming-home-to-vim
nnoremap / /\v
vnoremap / /\v

" Speed up srolling of viewport slightly
nnoremap <C-e> 2<C-e>
nnoremap <C-y> 2<C-y>

" Allow vim to set the window title in tmux
if exists('$TMUX')
  autocmd BufEnter * call system("tmux rename-window " . expand("%:t"))
  autocmd VimLeave * call system("tmux setw automatic-rename")
  autocmd BufEnter * let &titlestring = ' ' . expand("%:t")
end

" }}}
" => Folding Rules {{{
"""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""

set foldenable              " enable folding
set foldlevelstart=99       " start out with everything unfolded
set foldmethod=syntax       " fold based on the syntax
set foldminlines=0
set foldnestmax=10          " set a max level of nested folding to 10
set foldopen=block,hor,insert,jump,mark,percent,quickfix,search,tag,undo
                            " which commands trigger auto-fold

function! MyFoldText()
  let line = getline(v:foldstart)

  let nucolwidth = &fdc + &number * &numberwidth
  let windowwidth = winwidth(0) - nucolwidth - 2
  let foldedlinecount = v:foldend - v:foldstart

  " expand tabs into spaces
  let onetab = strpart('          ', 0, &tabstop)
  let line = substitute(line, '\t', onetab, 'g')

  let line = strpart(line, 0, windowwidth -2 - len(foldedlinecount))
  let fillcharcount = windowwidth - len(line) - len(foldedlinecount) - 4
  return line . ' …' . repeat(" ",fillcharcount) . foldedlinecount . ' '
endfunction
set foldtext=MyFoldText()

" }}}
" => Shortcut Mappings {{{
"""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""

" Set ; to be : as well
nnoremap ; :

" Avoid accidentally hitting F1
noremap! <F1> <Esc>

" Make p in visual mode replace the selected text with the yank register
vnoremap p <Esc>:let current_reg = @"<CR>gvdi<C-R>=current_reg<CR><Esc>

" Swap implementations of ` and ' jump to markers
" By default, ' jumps to marked line, ` jumps to the marked line and col
nnoremap ' `
nnoremap ` '

" Use the damn hjkl keys
noremap <up> <nop>
noremap <down> <nop>
noremap <left> <nop>
noremap <right> <nop>

" Remap j and k to act as expected when used on long, wrapped lines
nnoremap j gj
nnoremap k gk

" Easy window navigation
noremap <C-h> <C-w>h
noremap <C-j> <C-w>j
noremap <C-k> <C-w>k
noremap <C-l> <C-w>l

" Yank/paste to the OS clipboard
noremap <leader>y "+y
noremap <leader>Y "+yy
noremap <leader>p "+p
noremap <leader>P "+P

" Edit the vimrc file
nnoremap <silent> <leader>ev :e $MYVIMRC<CR>
nnoremap <silent> <leader>sv :so $MYVIMRC<CR>

" Clears the search register
nnoremap <silent> <leader>/ :nohlsearch<CR>

" Pull word under cursor into LHS of a substitute (for quick search/replace)
nnoremap <leader>z :%s#\<<C-r>=expand("<cword>")<CR>\>#

" Keep search matches in the middle of the window and pulse the line when
" moving to them
nnoremap n n:call PulseCursorLine()<CR>
nnoremap N N:call PulseCursorLine()<CR>

" Quickly get out of insert mode without moving your fingers
inoremap jk <Esc>

" Scratch
nnoremap <leader><tab> :Sscratch<CR><C-W>x<C-J>

" Reselect text that was just pasted
nnoremap <leader>v V`]

" Git bindings
nnoremap <leader>gb :Gblame<CR>
nnoremap <leader>gd :Gdiff<CR>

" Gundo.vim
nnoremap <leader>u :GundoToggle<CR>

" Make a more accessible <Esc> in insert mode
inoremap jk <Esc>

" Make > and < in visual mode return to visual mode after finishing
vnoremap < <gv
vnoremap > >gv

" Easy alignment
xmap ga <Plug>(EasyAlign)
nmap ga <Plug>(EasyAlign)

" Writing mode
noremap <leader>G :Goyo<CR>

" Running tests
nnoremap <silent> <leader>t :TestFile<CR>
nnoremap <silent> <leader>s :TestNearest<CR>
nnoremap <silent> <leader>l :TestLast<CR>
nnoremap <silent> <leader>a :TestSuite<CR>
nnoremap <silent> <leader>gt :TestVisit<CR>

" }}}
" => Plugin Configuration {{{
  " => ALE Settings {{{
  """""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""

  augroup ale_config
    autocmd!

    let g:ale_lint_on_text_changed = 0
    let g:ale_sign_column_always = 1
    let g:ale_sign_error = '✗'
    let g:ale_sign_warning = '⚠'

    set updatetime=1000
    autocmd CursorHold * call ale#Queue(0, 'lint_file')
    autocmd CursorHoldI * call ale#Queue(0, 'lint_file')
    autocmd InsertLeave * call ale#Queue(0, 'lint_file')
    autocmd TextChanged * call ale#Queue(0, 'lint_file')
  augroup END

  " }}}
  " => Dim-Inactive Settings {{{
  """""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""

  augroup diminactive_config
    autocmd!

    let g:diminactive_enable_focus = 1
  augroup END

  " }}}
  " => Elm Settings {{{
  """""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""

  augroup elm_config
    autocmd!

    let g:elm_format_autosave = 1
  augroup END

  " }}}
  " => Fzf Settings {{{
  """""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""

  augroup fzf_config
    autocmd!

    let g:fzf_layout = { 'down': '~25%' }

    if isdirectory(".git")
      nmap <silent> <leader>f :GFiles --cached --others --exclude-standard<cr>
    else
      nmap <silent> <leader>f :FZF<cr>
    endif

    nmap <silent> <leader>b :Buffers<cr>
    nmap <silent> <leader>F :FZFTags<cr>

    " Add fzf-powered exploration of key mappings
    nmap <leader><tab> <plug>(fzf-maps-n)
    omap <leader><tab> <plug>(fzf-maps-o)
    xmap <leader><tab> <plug>(fzf-maps-x)

    " Add fzf-powered completion
    imap <c-x><c-f> <plug>(fzf-complete-path)
    imap <c-x><c-j> <plug>(fzf-complete-file)
    imap <c-x><c-k> <plug>(fzf-complete-word)
    imap <c-x><c-l> <plug>(fzf-complete-line)

    " Add a quick colorscheme switcher
    nnoremap <silent> <leader>C :call fzf#run({
          \   'source':
          \     map(split(globpath(&rtp, "colors/*.vim"), "\n"),
          \         "substitute(fnamemodify(v:val, ':t'), '\\..\\{-}$', '', '')"),
          \   'sink':    'colo',
          \   'options': '+m',
          \   'left':    30
          \ })<CR>

    " Redefines the Find command to use ripgrep
    command! -bang -nargs=* Find call fzf#vim#grep(
          \ 'rg --column --line-number --no-heading --follow --color=always '.<q-args>, 1,
          \ <bang>0 ? fzf#vim#with_preview('up:60%') : fzf#vim#with_preview('right:50%:hidden', '?'), <bang>0)

    " Defines a tag search function using FZF
    function! s:tags_sink(line)
      let parts = split(a:line, '\t\zs')
      let excmd = matchstr(parts[2:], '^.*\ze;"\t')
      execute 'silent e' parts[1][:-2]
      let [magic, &magic] = [&magic, 0]
      execute excmd
      let &magic = magic
    endfunction

    function! s:tags()
      if empty(tagfiles())
        echohl WarningMsg
        echom 'Preparing tags'
        echohl None
        call system('ctags -R')
      endif

      call fzf#run({
            \ 'source':  'cat '.join(map(tagfiles(), 'fnamemodify(v:val, ":S")')).
            \            '| grep -v -a ^!',
            \ 'options': '+m -d "\t" --with-nth 1,4.. -n 1 --tiebreak=index',
            \ 'down':    '40%',
            \ 'sink':    function('s:tags_sink')})
    endfunction

    command! FZFTags call s:tags()
  augroup END

  " }}}
  " => FastFold Settings {{{
  """""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""

  augroup fastfold_config
    autocmd!

    let g:fastfold_savehook = 0
  augroup END

  " }}}
  " => GReplace Settings {{{
  """""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""

  augroup greplace_config
    autocmd!

    set grepprg=ag

    let g:grep_cmd_opts = '--line-numbers --noheading'
  augroup END

  " }}}
  " => Lightline Settings {{{
  """""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""

  augroup lightline_config
    autocmd!

    let g:lightline = {
      \ 'colorscheme': 'snazzy',
      \ 'active': {
      \   'left': [ [ 'mode', 'paste' ],
      \             [ 'gitbranch', 'readonly', 'filename', 'modified' ] ]
      \ },
      \ 'component_function': {
      \   'gitbranch': 'FugitiveHead'
      \ },
      \ }
  augroup END

  " }}}
  " => NERDTree Settings {{{
  """""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""

  augroup nerdtree_config
    autocmd!

    nnoremap <leader>n :NERDTreeToggle<CR>
    nnoremap <leader>N :NERDTreeFind<CR>

    " Store the bookmarks file
    let NERDTreeBookmarksFile=expand("$HOME/.vim/NERDTreeBookmarks")

    " Show the bookmarks table on startup
    let NERDTreeShowBookmarks=1

    " Show hidden files too
    let NERDTreeShowFiles=1
    let NERDTreeShowHidden=1
    let NERDTreeDirArrowExpandable = '▸'
    let NERDTreeDirArrowCollapsible = '▼'

    " Quit on opening files from the tree
    let NERDTreeQuitOnOpen=1

    " Highlight the selected entry in the tree
    let NERDTreeHighlightCursorline=1

    " Use a single click to fold/unfold directories and a double click to
    " open files
    let NERDTreeMouseMouse=2

    " Don't display these kinds of files
    let NERDTreeIgnore=[ '\.pyc$', '\.pyo$', '\.py\$class$', '\.obj$',
                        \ '\.o$', '\.so$', '\.egg$', '^\.git$' ]
  augroup END

  " }}}
  " => Testing Settings {{{
  """""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""

  let g:test#strategy = 'dispatch'
  let g:test#ruby#minitest#executable = 'm'

  " }}}
  " => Textobj Settings {{{
  """""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""

  augroup textobj_quote
    autocmd!

    map <silent> <leader>qc <Plug>ReplaceWithCurly
    map <silent> <leader>qs <Plug>ReplaceWithStraight

    autocmd FileType markdown call textobj#quote#init()
    autocmd FileType textile call textobj#quote#init()
    autocmd FileType text call textobj#quote#init({'educate': 0})
  augroup END

  " }}}
  " => UltiSnips Settings {{{
  """""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""

  augroup ultisnips_config
    autocmd!

    let g:UltiSnipsSnippetDirectories='~/.vim/UltiSnips'
    let g:UltiSnipsEditSplit='context'

  augroup END

  " }}}
  " => Writing Settings {{{
  augroup writing
    autocmd!

    let g:limelight_paragraph_span = 1
    let g:limelight_priority = -1

    function! s:goyo_enter()
      set linebreak
      set wrap
      Limelight
    endfunction

    function! s:goyo_leave()
      set nolinebreak
      set nowrap
      Limelight!
    endfunction

    autocmd User GoyoEnter call <SID>goyo_enter()
    autocmd User GoyoLeave call <SID>goyo_leave()
  augroup end
  " }}}
" }}}
" => Spell Checking {{{
"""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""

" Toggle and untoggle spell-check with <leader>ss
noremap <leader>ss :setlocal spell!<cr>

" Sane shortcuts
noremap <leader>sn ]s
noremap <leader>sp [s
noremap <leader>sa zg
noremap <leader>s? z=

" }}}
" => Filetype Specific Handling {{{
"""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""

if has("autocmd")
  augroup c_files " {{{
    autocmd!

    autocmd filetype c,cpp setlocal expandtab tabstop=4 softtabstop=4 foldmethod=marker foldmarker={,}
  augroup end " }}}
  augroup css_files " {{{
    autocmd!

    autocmd filetype css,less,scss setlocal expandtab shiftwidth=2 tabstop=2 softtabstop=2 foldmethod=marker foldmarker={,}
  augroup end " }}}
  augroup elm_files " {{{
    autocmd!

    autocmd filetype elm setlocal expandtab shiftwidth=4 tabstop=4 softtabstop=4
  augroup end " }}}
  augroup git_messages " {{{
    autocmd!

    autocmd filetype gitcommit setlocal spell spelllang=en_us
  augroup end " }}}
  augroup html_files " {{{
    autocmd!

    autocmd filetype html,html.ruby,xhtml,xml setlocal expandtab shiftwidth=2 tabstop=2 softtabstop=2

    " Auto-closing of HTML/XML tags
    let g:closetag_default_xml=1
    autocmd filetype html,html.ruby let b:closetag_html_style=1
  augroup end " }}}
  augroup javascript_files " {{{
    autocmd!

    autocmd filetype javascript setlocal expandtab shiftwidth=2 tabstop=2 softtabstop=2
    autocmd filetype javascript setlocal foldmethod=marker foldmarker={,}
    " autocmd filetype javascript call JavaScriptFold()
  augroup end " }}}
  augroup invisible_chars " {{{
    autocmd!

    " Show invisible characters in all of these files
    autocmd filetype vim setlocal list
    autocmd filetype python,rst setlocal list
    autocmd filetype ruby setlocal list
    autocmd filetype javascript,css setlocal list
    autocmd filetype html setlocal list
  augroup end " }}}
  augroup php_files " {{{
    autocmd!

    autocmd filetype php setlocal expandtab shiftwidth=4 tabstop=4 softtabstop=4
    autocmd filetype php setlocal textwidth=120
  augroup end " }}}
  augroup python_files " {{{
    autocmd!

    " PEP8 compliance (set 1 tab = 4 chars explicitly, even if set
    " earlier, as it is important)
    autocmd filetype python setlocal expandtab shiftwidth=4 tabstop=4 softtabstop=4
    autocmd filetype python setlocal textwidth=80
    autocmd filetype python match ErrorMsg '\%>80v.\+'

    " But disable autowrapping as it is super annoying
    autocmd filetype python setlocal formatoptions-=t

    " Folding for Python (uses syntax/python.vim for fold definitions)
    "autocmd filetype python,rst setlocal nofoldenable
    "autocmd filetype python setlocal foldmethod=expr

    " Python runners
    autocmd filetype python noremap <buffer> <F5> :w<CR>:!python %<CR>
    autocmd filetype python inoremap <buffer> <F5> <Esc>:w<CR>:!python %<CR>
    autocmd filetype python noremap <buffer> <S-F5> :w<CR>:!ipython %<CR>
    autocmd filetype python inoremap <buffer> <S-F5> <Esc>:w<CR>:!ipython %<CR>
  augroup end " }}}
  augroup rst_files " {{{
    autocmd!

    " Auto-wrap text around 74 characters
    autocmd filetype rst setlocal textwidth=74
    autocmd filetype rst setlocal formatoptions+=nqt
    autocmd filetype rst match ErrorMsg '\%>74v.\+'
  augroup end " }}}
  augroup ruby_files " {{{
    autocmd!

    autocmd filetype ruby setlocal expandtab shiftwidth=2 tabstop=2 softtabstop=2
  augroup end " }}}
  augroup snippet_files " {{{
    autocmd!

    autocmd filetype snippet setlocal shiftwidth=8 tabstop=8 softtabstop=8
  augroup end " }}}
  augroup textile_files " {{{
    autocmd!

    autocmd filetype textile set textwidth=78 wrap

    " Render YAML front matter inside Textile documents as comments
    autocmd filetype textile syntax region frontmatter start=/\%^---$/ end=/^---$/
    autocmd filetype textile highlight link frontmatter Comment
  augroup end " }}}
  augroup vim_files " {{{
    autocmd!

    " Bind <F1> to show the keyword under the cursor
    " General help can still be entered manually, with :help
    autocmd filetype vim noremap <buffer> <F1> <Esc>:help <C-r><C-w><CR>
    autocmd filetype vim noremap! <buffer> <F1> <Esc>:help <C-r><C-w><CR>
    autocmd filetype vim setlocal expandtab shiftwidth=2 tabstop=2 softtabstop=2
  augroup end " }}}
  augroup whitespace_chars " {{{
    autocmd!

    autocmd BufWritePre *.* :call <SID>StripTrailingWhitespace()
  augroup end " }}}
  augroup yaml_files " {{{
    autocmd!

    autocmd filetype yaml setlocal expandtab shiftwidth=2 tabstop=2 softtabstop=2
  augroup end " }}}
endif

" }}}
" => Python Mode Configuration {{{
"""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""

" Don't run pylint on every save - handled by Flake8
let g:pymode_lint = 0
let g:pymode_lint_write = 0

" }}}
" => Miscellaneous {{{
"""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""

" Don't use the cursor line in windows you aren't focused on
autocmd InsertLeave,WinEnter * set cursorline
autocmd InsertEnter,WinLeave * set nocursorline

" Restore cursor position upon reopening files, except for gitcommits
autocmd BufReadPost *
  \ if &ft != 'gitcommit' && line("'\"") > 0 && line("'\"") <= line("$") |
  \  exe "normal! g`\"" |
  \ endif

" Common abbreviations/misspellings
source ~/.vim/autocorrect.vim

" Helper function for search
function! PulseCursorLine()
  let current_window = winnr()

  windo set nocursorline
  execute current_window . 'wincmd w'

  setlocal cursorline

  redir => old_hi
  silent execute 'hi CursorLine'
  redir END
  let old_hi = split(old_hi, '\n')[0]
  let old_hi = substitute(old_hi, 'xxx', '', '')

  hi CursorLine guibg=#3a3a3a
  redraw
  sleep 20m

  hi CursorLine guibg=#4a4a4a
  redraw
  sleep 30m

  hi CursorLine guibg=#3a3a3a
  redraw
  sleep 30m

  hi CursorLine guibg=#2a2a2a
  redraw
  sleep 20m

  execute 'hi ' . old_hi

  windo set cursorline
  execute current_window . 'wincmd w'
endfunction

" Fix Windows ^M encoding problem
noremap <leader>m mmHmt:%s/<C-V><cr>//ge<cr>'tzt'm

" Strips trailing whitespace at the end of lines
function! <SID>StripTrailingWhitespace()
  let _s=@/
  let l = line(".")
  let c = col(".")
  %s/\s\+$//e
  let @/=_s
  call cursor(l, c)
endfunction

hi link javaScriptTemplateDelim String
hi link javaScriptTemplateVar Text
hi link javaScriptTemplateString String

" }}}
" vim:foldmethod=marker:foldlevel=0
